apply plugin: 'com.android.library'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 20053
        versionName '2.0.53'
    }

    defaultPublishConfig "release"
    publishNonDefault false

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        androidTest{
            java.srcDirs = ['androidTest']
        }
    }
}

dependencies {
    compile 'com.google.android.gms:play-services-base:6.5.87'
    compile project(':dependencyLibs:Mavlink')
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.getName()
    def task = project.tasks.create "jar${name.capitalize()}", Jar

    task.dependsOn variant.javaCompile
    task.dependsOn variant.processJavaResources

    task.from variant.javaCompile.destinationDir
    task.from variant.processJavaResources.destinationDir
    task.from android.sourceSets.main.java.srcDirs

    //Include dependent jars with some exceptions
    task.from configurations.compile.findAll {
        it.getName() != 'android.jar' && !it.getName().startsWith('junit') && !it.getName().startsWith('hamcrest')
    }.collect {
        it.isDirectory() ? it : zipTree(it)
    }

    task.setDestinationDir(new File("${rootDir}/ClientLib/mobile/libs"))

    artifacts.add('archives', task);
}